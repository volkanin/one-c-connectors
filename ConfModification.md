Здесь будут описывается модификации конфигурации 1С, необходимые для использования OneCService2

# Модуль внешнего соединения #

Надо добавить:
```
Функция OneCService_ВыполнитьСтроку(_стр) Экспорт		
	результат = Неопределено;
	Выполнить(_стр);
	Возврат результат;		
КонецФункции

Функция OneCService_ПринадлежитТипу(_значение, _тип) Экспорт
	Возврат _тип = ТипЗнч(_значение);
КонецФункции

Функция OneCService_ЭлементыСтруктурыВМассив(_структура) Экспорт 
	м = Новый Массив();
	Для Каждого э Из _структура Цикл
		м.Добавить(э);
	КонецЦикла;
	Возврат м;
КонецФункции

Функция OneCService_ЭтоXDTO(_значение) Экспорт
	Попытка
		uri = _значение.Тип().URIПространстваИмен;
		Возврат Истина;
	Исключение
		Возврат Ложь;
	КонецПопытки;
КонецФункции
```

В связи с начатой реструктуризацией пропорции между 1С и .NET частями сильно сдвинулись в сторону 1С, пока относительно рабочий вариант кода имеет вид:


```
//****************************************************************************************************
//												Служебные методы
//****************************************************************************************************
Функция OneCService2_ОпределитьТип(_значение) Экспорт
	Если ТипЗнч(_значение) = Тип("Строка") Тогда
		Возврат "STRING";
	ИначеЕсли ТипЗнч(_значение) = Тип("Число") Тогда
		Возврат "DOUBLE";
	ИначеЕсли ТипЗнч(_значение) = Тип("Булево") Тогда
		Возврат "BOOLEAN";
	ИначеЕсли ТипЗнч(_значение) = Тип("Дата") Тогда
		Возврат "DATE";
	ИначеЕсли ТипЗнч(_значение) = Тип("Массив") Тогда
		Возврат "ARRAY";
	ИначеЕсли ТипЗнч(_значение) = Тип("Структура") Тогда
		Возврат "STRUCT";
	Иначе 
		Возврат "OBJECT";
	КонецЕсли;
КонецФункции

Процедура OneCService2_СериализоватьМассив(_xdto, _записьXML, _массив, _dataHelper) Экспорт	
	_записьXML.ЗаписатьНачалоЭлемента(_dataHelper.GetArrayElementName());	
	Для Каждого э Из _массив Цикл		
		_записьXML.ЗаписатьНачалоЭлемента(_dataHelper.GetArrayElementName()+"-item");
		OneCService2_Сериализовать(_xdto, _записьXML, э, OneCService2_ОпределитьТип(э), _dataHelper);
		_записьXML.ЗаписатьКонецЭлемента();
	КонецЦикла;
	_записьXML.ЗаписатьКонецЭлемента();	
КонецПроцедуры

Процедура OneCService2_СериализоватьСтруктуру(_xdto, _записьXML, _структура, _dataHelper) Экспорт
	_записьXML.ЗаписатьНачалоЭлемента(_dataHelper.GetStructElementName());	
	Для Каждого э Из _структура Цикл		
		//Элемент структуры
		_записьXML.ЗаписатьНачалоЭлемента(_dataHelper.GetStructElementName()+"-item");	
		
		//Ключ
		_записьXML.ЗаписатьНачалоЭлемента(_dataHelper.GetStructKeyElementName());	
		OneCService2_Сериализовать(_xdto, _записьXML, э.Ключ, OneCService2_ОпределитьТип(э.Ключ), _dataHelper);
		_записьXML.ЗаписатьКонецЭлемента();
		//Значение
		_записьXML.ЗаписатьНачалоЭлемента(_dataHelper.GetStructValueElementName());			
		OneCService2_Сериализовать(_xdto, _записьXML, э.Значение, OneCService2_ОпределитьТип(э.Значение), _dataHelper);		
		_записьXML.ЗаписатьКонецЭлемента();
		
		_записьXML.ЗаписатьКонецЭлемента();
	КонецЦикла;
	_записьXML.ЗаписатьКонецЭлемента();	
КонецПроцедуры

Процедура OneCService2_СериализоватьОбъект(_xdto, _записьXML, _объект, _dataHelper) Экспорт
	 _xdto.ЗаписатьXML(_записьXML, _объект);
КонецПроцедуры 

Процедура OneCService2_Сериализовать(_xdto, _записьXML, _значение, _ocsТип, _dataHelper, _первыйВызов=Ложь) Экспорт
	Если _ocsТип = "ARRAY" Тогда
		OneCService2_СериализоватьМассив(_xdto, _записьXML, _значение, _dataHelper);	
	ИначеЕсли _ocsТип = "STRUCT" Тогда
		OneCService2_СериализоватьСтруктуру(_xdto, _записьXML, _значение, _dataHelper);
	ИначеЕсли _ocsТип = "OBJECT" Тогда
		об = _значение;
		Попытка
			об = об.ПолучитьОбъект();
		Исключение
		КонецПопытки;
		OneCService2_СериализоватьОбъект(_xdto, _записьXML, об, _dataHelper);	
	ИначеЕсли _ocsТип = "Date" Тогда
		Если _первыйВызов Тогда
			_записьXML.ЗаписатьНачалоЭлемента(_dataHelper.GetRemoveThisElementName());
		КонецЕсли;
		_записьXML.ЗаписатьТекст(_dataHelper.FormatDate(_значение));
		Если _первыйВызов Тогда
			_записьXML.ЗаписатьКонецЭлемента();
		КонецЕсли;
	Иначе
		Если _первыйВызов Тогда
			_записьXML.ЗаписатьНачалоЭлемента(_dataHelper.GetRemoveThisElementName());
		КонецЕсли;
		_записьXML.ЗаписатьТекст(""+_значение);
		Если _первыйВызов Тогда
			_записьXML.ЗаписатьКонецЭлемента();
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

Функция OneCService2_ЭтоМассив(_чтениеXML, _dataHelper) Экспорт	
	Если _чтениеXML.ЛокальноеИмя = _dataHelper.GetArrayElementName() Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
КонецФункции

Функция OneCService2_ДесериализоватьМассив(_xdto, _чтениеXML, _dataHelper) Экспорт
	массив = Новый Массив();
	//Читаем пока есть что читать и пока не достигли закрывающего элемента
	Пока (_чтениеXML.Прочитать()) И 
		 (
		 	НЕ (
					(_чтениеXML.ТипУзла = ТипУзлаXML.КонецЭлемента) И 
					(_чтениеXML.ЛокальноеИмя = _dataHelper.GetArrayElementName())
			   )
		 ) Цикл
		//Здесь мы находимся на onecservice-array-item
		Если _чтениеXML.ЛокальноеИмя <> (_dataHelper.GetArrayElementName()+"-item") Тогда
			ВызватьИсключение "Ошибка в XML представлении массива: ожидалось "+
							  (_dataHelper.GetArrayElementName()+"-item")+" а получили "+_чтениеXML.ЛокальноеИмя;							  
		КонецЕсли;		
		Если _чтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда
			//Читаем содержимое
			_чтениеXML.Прочитать();
			Если _чтениеXML.ТипУзла = ТипУзлаXML.Текст Тогда
				//Примитив	
				массив.Добавить(_dataHelper.DeSerializeSimpleString(_чтениеXML.Значение));
			Иначе
				//Сложные тип
				массив.Добавить(OneCService2_Десериализовать(_xdto, _чтениеXML, _dataHelper));
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	Возврат массив;
КонецФункции

Функция OneCService2_ЭтоСтруктура(_чтениеXML, _dataHelper) Экспорт
	Если _чтениеXML.ЛокальноеИмя = _dataHelper.GetStructElementName() Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
КонецФункции

Функция OneCService2_ДесериализоватьСтруктуру(_xdto, _чтениеXML, _dataHelper) Экспорт
	структура = Новый Структура();
	//Читаем пока есть что читать и пока не достигли закрывающего элемента
	//!!!_dataHelper.ForDebug("SSS0 "+_чтениеXML.ЛокальноеИмя);
	Пока (_чтениеXML.Прочитать()) И
		 (
		 	Не (
					(_чтениеXML.ТипУзла = ТипУзлаXML.КонецЭлемента) И
					(_чтениеXML.ЛокальноеИмя = _dataHelper.GetStructElementName())
			    )
		 ) Цикл
		//Здесь мы находимся на onecservice-struct-item
		//!!!_dataHelper.ForDebug("SSS2 "+_чтениеXML.ЛокальноеИмя+"|"+_чтениеXML.ТипУзла);
		Если _чтениеXML.ЛокальноеИмя <> (_dataHelper.GetStructElementName()+"-item") Тогда
			ВызватьИсключение "Ошибка в XML представлении массива: ожидалось "+
							  _dataHelper.GetStructElementName()+"-item а получили "+_чтениеXML.ЛокальноеИмя;
		Иначе 		
							  
			ключ = Неопределено;
			значение = Неопределено;
			 
			//Читаем struct-item-key
			_чтениеXML.Прочитать();	
			//Читаем содержимое
			_чтениеXML.Прочитать();	
			//!!!_dataHelper.ForDebug("SSS31 "+_чтениеXML.ЛокальноеИмя);
			Если _чтениеXML.ТипУзла = ТипУзлаXML.Текст Тогда
				//Примитив	
				ключ = _dataHelper.DeSerializeSimpleString(_чтениеXML.Значение);
			Иначе
				//Сложные тип
				ключ = OneCService2_Десериализовать(_xdto, _чтениеXML, _dataHelper);
			КонецЕсли;
			
			//Читаем struct-item-value						
			_чтениеXML.Прочитать();			
			//!!!_dataHelper.ForDebug("SSS32 "+_чтениеXML.ЛокальноеИмя);
			Если (Не _чтениеXML.Прочитать()) ИЛИ (_чтениеXML.ЛокальноеИмя <> _dataHelper.GetStructValueElementName()) Тогда
				ВызватьИсключение "Ошибка в XML представлении массива: ожидалось "+
							  _dataHelper.GetStructValueElementName()+" а получили "+_чтениеXML.ЛокальноеИмя;
			Иначе

				//Читаем содержимое
				_чтениеXML.Прочитать();	
				
            	Если _чтениеXML.ТипУзла = ТипУзлаXML.Текст Тогда
					//Примитив	
					значение = _dataHelper.DeSerializeSimpleString(_чтениеXML.Значение);
				Иначе
					//Сложные тип
					значение = OneCService2_Десериализовать(_xdto, _чтениеXML, _dataHelper);
				КонецЕсли;
			КонецЕсли;
			
			//Читаем завершающий struct-item-value			
			_чтениеXML.Прочитать();
			//Читаем завершающий onecservice-struct-item
			_чтениеXML.Прочитать();
			
			//!!!_dataHelper.("SSS "+ключ+"|"+значение);
			структура.Вставить(ключ, значение);
		КонецЕсли; //Если struct-item-key
	КонецЦикла;
	Возврат структура;
КонецФункции

Функция OneCService2_Десериализовать(_xdto, _чтениеXML, _dataHelper) Экспорт	
	//!!!_dataHelper.ForDebug("QQQ1 "+_чтениеXML.ЛокальноеИмя);
	//Спозицируемся на начале элемента
	Если _чтениеXML.ТипУзла <> ТипУзлаXML.НачалоЭлемента Тогда
		Пока (_чтениеXML.Прочитать()) И (_чтениеXML.ТипУзла <> ТипУзлаXML.НачалоЭлемента) Цикл
		КонецЦикла;	 		
	КонецЕсли;
	
	//!!!_dataHelper.ForDebug("QQQ2 "+_чтениеXML.ЛокальноеИмя);
	//Здесь обрабатываются только коллекции и сложные типы, примитивные типы обрабатываются только как их элементы
	Если OneCService2_ЭтоМассив(_чтениеXML, _dataHelper) Тогда	
		//Массив
		Возврат OneCService2_ДесериализоватьМассив(_xdto, _чтениеXML, _dataHelper);
	ИначеЕсли OneCService2_ЭтоСтруктура(_чтениеXML, _dataHelper) Тогда
		//Структура
		Возврат OneCService2_ДесериализоватьСтруктуру(_xdto, _чтениеXML, _dataHelper);
	Иначе
		//Объект
		Возврат _xdto.ПрочитатьXML(_чтениеXML);
	КонецЕсли;
КонецФункции

Процедура OneCService2_ВыполнитьСкрипт(_скрипт, _resultSet, _dataHelper) Экспорт
	xdto = Новый СериализаторXDTO(ФабрикаXDTO);
	результат = Неопределено;	
	//Выполним скрипт
	Выполнить(_скрипт);	
	Если результат = Неопределено Тогда
		результат = "";
	КонецЕсли;
	
	//Заполним результат	
	записьXML = Новый ЗаписьXML();	
	записьXML.УстановитьСтроку();
	Попытка
		
		ocsТип = OneCService2_ОпределитьТип(результат);
		_resultSet.AddColumnName("value");
		_resultSet.AddColumnType(ocsТип);
	
		row = _resultSet.CreateRow();
	
		OneCService2_Сериализовать(xdto, записьXML, результат, ocsТип, _dataHelper, Истина);	
		row.AddValue(_dataHelper.XmlNodeFromString(записьXML.Закрыть()), _dataHelper);		
	Исключение
		записьXML.Закрыть();
		ВызватьИсключение ОписаниеОшибки();
	КонецПопытки;

КонецПроцедуры

Процедура OneCService2_ВыполнитьЗапрос(_текстЗапроса, _resultSet, _dataHelper) Экспорт	
	xdto = Новый СериализаторXDTO(ФабрикаXDTO);	
	запрос = Новый Запрос(_текстЗапроса);
	результатЗапроса = запрос.Выполнить();
	Если Не результатЗапроса.Пустой() Тогда
		//Заполним информацию по колонкам
		количествоКолонок = результатЗапроса.Колонки.Количество();
		Для i=0 По (количествоКолонок-1) Цикл
			_resultSet.AddColumnName(результатЗапроса.Колонки.Получить(i).Имя);
		КонецЦикла;
		//Заполним строки
		выборка = результатЗапроса.Выбрать();
		этоПервыйПроход = Истина;		
		Пока выборка.Следующий() Цикл
			row = _resultSet.CreateRow();
			Для i=0 По (количествоКолонок-1) Цикл				
				значение = выборка.Получить(i);
				ocsТип = OneCService2_ОпределитьТип(значение);
				//Зададим тип, если он еще не задан
				Если этоПервыйПроход Тогда
					_resultSet.AddColumnType(ocsТип);
				КонецЕсли;
				
				//Сериализуем значение				
				записьXML = Новый ЗаписьXML();	
				записьXML.УстановитьСтроку();

				Попытка					
					OneCService2_Сериализовать(xdto, записьXML, значение, ocsТип, _dataHelper, Истина);	
					row.AddValue(_dataHelper.XmlNodeFromString(записьXML.Закрыть()), _dataHelper);
				Исключение
					записьXML.Закрыть();
					ВызватьИсключение ОписаниеОшибки();
				КонецПопытки;			
			КонецЦикла;//Для i
		КонецЦикла;// Пока выборка
		этоПервыйПроход = Ложь;
	КонецЕсли;
КонецПроцедуры

Процедура OneCService2_ВыполнитьМетод(_имяМетода, _xmlNodeParameters, _resultSet, _dataHelper) Экспорт	
	xdto = Новый СериализаторXDTO(ФабрикаXDTO);
	//Десериализуем параметры
	параметры = Новый Массив();
	Для Каждого xmlParameter Из _xmlNodeParameters Цикл
		значениеПараметра = Неопределено;		
		Если _dataHelper.IsSimple(xmlParameter) Тогда
			//Примитивы десериализуем напрямую через _dataHelper
			значениеПараметра = _dataHelper.DeSerializeSimple(xmlParameter);		
		Иначе
			//Сложные типы и коллекции - средcтвами 1С
			чтениеXML = Новый ЧтениеXML();
			//!!!_dataHelper.ForDebug("WWW "+_dataHelper.StringFromXmlNode(xmlParameter));
			чтениеXML.УстановитьСтроку(_dataHelper.StringFromXmlNode(xmlParameter));		
			Попытка
				значениеПараметра = OneCService2_Десериализовать(xdto, чтениеXML, _dataHelper);				
			Исключение
				чтениеXML.Закрыть();
				ВызватьИсключение ОписаниеОшибки();
			КонецПопытки;			
		КонецЕсли;
		параметры.Добавить(значениеПараметра);
	КонецЦикла;
	
	//Вызовем метод	
	результат = Неопределено;
	строкаВызоваМетода = "результат = " + _имяМетода + "(";
	Для i=0 По (параметры.Количество()-1) Цикл
		строкаВызоваМетода = строкаВызоваМетода + "параметры["+i+"]";
		Если i<(параметры.Количество()-1) Тогда
			строкаВызоваМетода = строкаВызоваМетода + ",";
		КонецЕсли;
	КонецЦикла;
	строкаВызоваМетода = строкаВызоваМетода + ")";	
	Выполнить строкаВызоваМетода;
	//Сериализуем результат если он есть
	Если результат <> Неопределено Тогда
		ocsТип = OneCService2_ОпределитьТип(результат);		
		
		записьXML = Новый ЗаписьXML();	
		записьXML.УстановитьСтроку();
		Попытка			
			row = _resultSet.CreateRow();
			
			_resultSet.AddColumnName("value");
			_resultSet.AddColumnType(ocsТип);
			
			OneCService2_Сериализовать(xdto, записьXML, результат, ocsТип, _dataHelper, Истина);			
			row.AddValue(_dataHelper.XmlNodeFromString(записьXML.Закрыть()), _dataHelper);
		Исключение
			записьXML.Закрыть();
			ВызватьИсключение ОписаниеОшибки();
		КонецПопытки;
	КонецЕсли;
КонецПроцедуры


//****************************************************************************************************
//												Тестовые методы
//****************************************************************************************************
Функция OneCService2_ТестовыйМетод(_число, _строка) Экспорт
	Возврат ""+_число+"|"+_строка;
КонецФункции

Функция OneCService2_ТестовыйМетод2(_строка, _массив) Экспорт
	Возврат ""+_строка+"|"+_массив[0];
КонецФункции

Функция OneCService2_ТестовыйМетод3(_строка, _структура) Экспорт
	Возврат ""+_строка+"|"+_структура.Ключ1+"|"+_структура.Ключ2.Наименование;
КонецФункции
```

возможно его можно сильно переработать и упростить (скорее всего так и получится), и да, как и предлагал Алексей, его можно оформить общим модулем с флажками. Как видно из кода, основная идея состоит в том, чтобы вынести ту часть логики, которая раньше дергалась через COM в код 1С, уменьшим количество вызовов COM'а и разграничив жизненный цикл: все переменные, которые принадлежат 1С, там же и используются, их область действия полностью прозрачна с точки зрения 1С (за ее границами никто к ним не обратится извне, напоровшись на SEHExcpetion/Access violation, если их подчистил GC 1С) + в коде 1С доступен дополнительный функционал предоставляемый OCS2 - DataHelper, который тоже имеет четкую область действия с точки зрения как .NET, так и 1С. Весь результат упаковывается в ResultSet. В общем это точно не последняя версия, буду копать дальше.